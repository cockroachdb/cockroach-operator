load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("@io_bazel_rules_docker//docker/util:run.bzl", "container_run_and_commit_layer")

go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/cockroachdb/cockroach-operator/cmd/cockroach-operator",
    visibility = ["//visibility:private"],
    deps = [
        "//apis/v1alpha1:go_default_library",
        "//pkg/controller:go_default_library",
        "//pkg/utilfeature:go_default_library",
        "@io_k8s_apimachinery//pkg/runtime:go_default_library",
        "@io_k8s_client_go//kubernetes/scheme:go_default_library",
        "@io_k8s_client_go//plugin/pkg/client/auth/gcp:go_default_library",
        "@io_k8s_sigs_controller_runtime//:go_default_library",
        "@io_k8s_sigs_controller_runtime//pkg/log/zap:go_default_library",
    ],
)

go_binary(
    name = "cockroach-operator",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "licenses",
    srcs = ["//:license"],
    mode = "0755",
    package_dir = "/licenses",
)

container_run_and_commit_layer(
    name = "ubi_update",
    commands = [
        "microdnf install yum",
        "yum -y update-minimal --security --sec-severity=Important --sec-severity=Critical",
        "microdnf remove yum && microdnf clean all",
    ],
    image = "@redhat_ubi_minimal//image",
)

container_image(
    name = "ubi_base_image",
    # References container_pull from WORKSPACE
    base = "@redhat_ubi_minimal//image",
    labels = {
        "name": "CockroachDB Operator",
        "vendor": "Cockroach Labs",
        "release": "{NUMBER_COMMITS_ON_BRANCH}",
        "version": "{STABLE_DOCKER_TAG}",
        "summary": "CockroachDB is a distributed SQL database",
        "description": "CockroachDB is a PostgreSQL wire-compatible distributed SQL database",
    },
    stamp = True,
    tars = [
        ":licenses",
        ":ubi_update",
    ],
)

pkg_tar(
    name = "manifests-tar",
    srcs = ["//deploy/certified-metadata-bundle/cockroach-operator/latest/manifests"],
    mode = "0755",
    package_dir = "/manifests",
)

pkg_tar(
    name = "metadata-tar",
    srcs = ["//deploy/certified-metadata-bundle/cockroach-operator/latest/metadata"],
    mode = "0755",
    package_dir = "/metadata",
)

container_image(
    name = "operator_bundle_image",
    # References container_pull from WORKSPACE
    # base = "@redhat_ubi_minimal//image",
    labels = {
        "operators.operatorframework.io.bundle.mediatype.v1": "registry+v1",
        "operators.operatorframework.io.bundle.manifests.v1": "manifests/",
        "operators.operatorframework.io.bundle.metadata.v1": "metadata/",
        "operators.operatorframework.io.bundle.package.v1": "cockroachdb-certified",
        "operators.operatorframework.io.bundle.channels.v1": "beta,stable",
        "operators.operatorframework.io.bundle.channel.default.v1": "stable",
        "com.redhat.openshift.versions": "v4.5,v4.6",
        "com.redhat.delivery.backport": "true",
        "com.redhat.delivery.operator.bundle": "true",
    },
    stamp = True,
    tars = [
        ":manifests-tar",
        ":metadata-tar",
    ],
    visibility = ["//visibility:public"],
)

#  fetch_crdb downloads the cockroach binary
genrule(
    name = "fetch_crdb_container",
    srcs = ["@crdb_linux//:file"],
    outs = ["cockroach"],
    cmd = "cp $(SRCS) $@",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "cockroach-tar",
    srcs = [":fetch_crdb_container"],
    mode = "0755",
    package_dir = "/usr/local/bin",
)

# include cockroach in an image
container_image(
    name = "cockroach_image",
    base = ":ubi_base_image",
    tars = [
        ":cockroach-tar",
    ],
)

go_image(
    name = "operator_image",
    base = ":cockroach_image",  # include the cr binary
    binary = ":cockroach-operator",
    visibility = ["//visibility:public"],
)

filegroup(
    name = "package-srcs",
    srcs = glob(["**"]),
    tags = ["automanaged"],
    visibility = ["//visibility:private"],
)

filegroup(
    name = "all-srcs",
    srcs = [":package-srcs"],
    tags = ["automanaged"],
    visibility = ["//visibility:public"],
)
